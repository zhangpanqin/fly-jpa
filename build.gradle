buildscript {
    ext {
        springDocOpenapiUiVersion = '1.5.10'
        zonkySpringTestVersion = '2.1.0'
        zonkyPostgresVersion = '1.3.1'
        mockitoInlineVersion = '3.12.4'
    }
}
plugins {
    id 'org.springframework.boot' version '2.6.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "com.avast.gradle.docker-compose" version "0.14.9" apply false
    id 'java'
    id 'idea'
    id 'org.unbroken-dome.test-sets' version '4.0.0'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'
java {
    sourceCompatibility JavaVersion.VERSION_11
    targetCompatibility JavaVersion.VERSION_11
}
configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}
testSets {
    isolationTest
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.flywaydb:flyway-core'
    implementation 'cn.hutool:hutool-all:5.7.16'
    implementation 'org.mapstruct:mapstruct:1.4.2.Final'
    implementation "org.springdoc:springdoc-openapi-ui:$springDocOpenapiUiVersion"
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.mockito:mockito-inline:$mockitoInlineVersion"
    // in order to use main and test of source code in isolationTest
    isolationTestImplementation sourceSets.test.output
    isolationTestImplementation 'io.rest-assured:spring-mock-mvc'
    isolationTestImplementation "io.zonky.test:embedded-database-spring-test:$zonkySpringTestVersion"
    isolationTestImplementation "io.zonky.test:embedded-postgres:$zonkyPostgresVersion"
}

tasks.withType(Test) {
    useJUnitPlatform()
}

wrapper {
    gradleVersion "7.3"
}
isolationTest.shouldRunAfter test
apply from: "${rootProject.projectDir}/gradle/docker-compose/docker-compose.gradle"

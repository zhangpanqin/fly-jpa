plugins {
    id 'java'
    id 'org.springframework.boot' apply false
    id 'com.avast.gradle.docker-compose' apply false
    id 'org.unbroken-dome.test-sets' apply false
}

group 'com.mflyyou.jpa'
version '0.0.1'

allprojects {
    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'

    configurations {
        parentBom
        annotationProcessor.extendsFrom(parentBom)
        compileOnly.extendsFrom(parentBom, annotationProcessor)
        implementation.extendsFrom(parentBom)
    }

    configurations.configureEach {
        resolutionStrategy {
            force 'io.zonky.test:embedded-database-spring-test:2.2.0'
            force 'io.zonky.test:embedded-postgres:2.0.3'
            force 'org.mapstruct:mapstruct:1.5.3.Final'
            force 'org.mapstruct:mapstruct-processor:1.5.3.Final'
            force 'org.mapstruct:mapstruct-processor:1.5.3.Final'
            force 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
        }
    }

    dependencies {
        parentBom platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
        parentBom platform('io.zonky.test.postgres:embedded-postgres-binaries-bom:14.7.0')
        compileOnly 'org.projectlombok:lombok'
        implementation 'org.mapstruct:mapstruct'
        annotationProcessor 'org.mapstruct:mapstruct-processor'
        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok-mapstruct-binding'
    }

    compileJava {
        options.compilerArgs.add '-AaddGeneratedAnnotation=true'
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
    }
}

apply from: "${rootProject.projectDir}/gradle/docker-compose/docker-compose.gradle"
